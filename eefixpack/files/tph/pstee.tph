/////                                                  \\\\\
///// string fixes                                     \\\\\
/////                                                  \\\\\

// string fixes, processed automatically if tra file exists
LAUNCH_ACTION_FUNCTION cd_string_set_from_tra
  STR_VAR input_tra  = EVAL ~eefixpack/languages/%LANGUAGE%/fixes_%game%.tra~ // tra file to process
          output_tph = EVAL ~weidu_external/eefixpack/fixes_%game%.tph~ // tph being built; use weidu_external for this
END

/////                                                  \\\\\
///// ids/2da fixes                                    \\\\\
/////                                                  \\\\\

COPY_EXISTING ~animate.ids~ ~override~
  REPLACE_TEXTUALLY ~0xf005[ %TAB%]+cl_4_Collector_Thug~ ~0xf006 cl_4_Collector_Thug~
  REPLACE_TEXTUALLY ~0xf042[ %TAB%]+Trelon~ ~0xf043 Trelon~
BUT_ONLY

INCLUDE ~eefixpack/files/tph/clswpbon.tpa~ // tbd, cam: fix non-prof penalties for shadowdancers, mage-thieves
INCLUDE ~eefixpack/files/tph/a7/pst_ini_extra.tph~  // extra animation slots for special characters

/////                                                  \\\\\
///// mass copy/includes actions                       \\\\\
/////                                                  \\\\\

/////                                                  \\\\\
///// dialogue fixes                                   \\\\\
/////                                                  \\\\\

COMPILE ~eefixpack/files/d/%game%_core_fixes.d~ // misc dialogue fixes

/////                                                  \\\\\
///// script fixes                                     \\\\\
/////                                                  \\\\\

COMPILE ~eefixpack/files/baf/pst~

INCLUDE ~eefixpack/files/tph/pst_script_fixes.tph~

/////                                                  \\\\\
///// area fixes                                       \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/pst_are_fixes.tph~ // ARE-specific fixes
INCLUDE ~eefixpack/files/tph/pst_wed_fixes.tph~ // WED-specific fixes

/////                                                  \\\\\
///// creature file fixes                              \\\\\
/////                                                  \\\\\

// Fixing creature script names
COPY_EXISTING ~apprent2.cre~ ~override~
              ~apprent3.cre~ ~override~
              ~apprent4.cre~ ~override~
              ~apprent5.cre~ ~override~
  WRITE_ASCII DEATHVAR ~None~ (32)
BUT_ONLY

// Fixing creature race
ACTION_DEFINE_ASSOCIATIVE_ARRAY cre_race BEGIN
  // resref   => RACE.IDS value
  ~bsohmien~  => 59   // SOHMIEN
  ~hbdygrd~   => 1    // HUMAN
  ~lady~      => 38   // LADY_OF_PAIN
  ~trias~     => 14   // DEVA
  ~triasa~    => 14   // DEVA
  ~triasb~    => 14   // DEVA
  ~triasc~    => 14   // DEVA
  ~worm~      => 71   // WORM
END

ACTION_PHP_EACH cre_race AS resref => race BEGIN
  COPY_EXISTING ~%resref%.cre~ ~override~
    WRITE_BYTE 0x272 race
  BUT_ONLY
END

INCLUDE ~eefixpack/files/tph/pst_cre_color_fixes.tph~ // creature color fixes

/////                                                  \\\\\
///// item file fixes                                  \\\\\
/////                                                  \\\\\

// The Number of Ku'u Yin should protect from Chaotic creatures
COPY_EXISTING ~kyname.itm~ ~override~
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 0 END   // Remove: AC bonus
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 33 END  // Remove: Save vs. Death bonus
  PATCH_FOR_EACH align IN ~49~ ~50~ ~51~ BEGIN // alignment: chaotic good/neutral/evil
    LPF ADD_ITEM_EQEFFECT
    INT_VAR
      opcode        = 219   // AC vs. Creature Type Modifier
      target        = 1     // Self
      timing        = 2     // While equipped
      parameter1    = align // IDS value
      parameter2    = 8     // IDS target: ALIGN.IDS
    END
  END
BUT_ONLY

/////                                                  \\\\\
///// spell fixes                                      \\\\\
/////                                                  \\\\\

/////                                                  \\\\\
///// store fixes                                      \\\\\
/////                                                  \\\\\

/////                                                  \\\\\
///// misc/other fixes                                 \\\\\
/////                                                  \\\\\

// removing reference to invalid game file
DISABLE_FROM_KEY ~.bcs~

INCLUDE ~eefixpack/files/tph/pst_bam_fixes.tph~ // BAM-specific fixes

/////                                                  \\\\\
///// final batch of INCLUDEs and cross-patching       \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_pstee_probabilities.tph~ // ie-6021 & ie-6037, cam: probability fixes
